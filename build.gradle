import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.17.4'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'org.jetbrains.kotlin.kapt' version '2.1.0'
}

ext {
    intellijPlatform = 'IU-2023.3'
    intellijPlatformPlugins = ['java', 'DatabaseTools'] // DatabaseTools is for BPMN process 'debugging'

    kotlinStdlib = '2.1.0'
    kotlinApiVersion = '1.8' // It must be same as JB supplied Kotlin STDLIB i.e. for 2018 = 1.3. See https://youtrack.jetbrains.com/issue/KT-37435

    mapstruct = '1.4.2.Final'
    jackson = '2.15.2'
    dom4j = '2.0.0'

    junitJupiter = '5.6.2'
    jsonPath = '2.8.0'
    junitPlatformRunner = '1.6.2'
    kluent = '1.68'
    mockitoInline = '5.2.0'
    mockitoKotlin = '2.2.0'
}

group = 'com.valb3r'

sourceCompatibility = 17

repositories {
    mavenCentral()
}

intellij {
    version = intellijPlatform
    plugins = intellijPlatformPlugins
}

patchPluginXml.enabled = false
verifyPlugin.enabled = false
publishPlugin.enabled = false

allprojects {
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.8.12'
    }
}



subprojects {
    tasks.withType(Test) {
        useJUnitPlatform()
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
            apiVersion = kotlinApiVersion
        }
    }
}
