plugins {
    id 'java'
    id 'org.jetbrains.intellij'
    id 'org.jetbrains.kotlin.jvm'
}

group 'com.valb3r.bpmn.intellij.plugin.core'

sourceCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":xml-parser-api")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation("org.apache.xmlgraphics:batik-transcoder:1.16") { // JB has removed batik from IC 2023.2
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson"

    // Tests
    testImplementation project(':flowable-xml-parser')
    testImplementation project(':camunda-xml-parser')
    testImplementation project(":xml-parser-core")
    testImplementation project(':bpmn-intellij-plugin-common-tests')
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitJupiter"
    testImplementation "org.junit.platform:junit-platform-runner:$junitPlatformRunner"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiter"
    testImplementation "org.amshove.kluent:kluent:$kluent"
    testImplementation "org.mockito:mockito-inline:$mockitoInline"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlin"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

intellij {
    version = intellijPlatform
    updateSinceUntilBuild = false
    plugins = intellijPlatformPlugins
}

patchPluginXml.enabled = false
verifyPlugin.enabled = false
publishPlugin.enabled = false

clean {
    delete 'out'
}

test {
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
        includeNoLocationClasses = true
        excludes = ["jdk.internal.*"]
    }

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}



jacocoTestReport {
    // Adjust the output of the test report
    reports {
        xml.required = true
        csv.required = true
    }
}
